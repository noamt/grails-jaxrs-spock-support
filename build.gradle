buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.1.0'
    }
}

apply plugin: 'artifactory-publish'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'groovy'

repositories {
    maven { url 'http://repo.grails.org/grails/core' }
    maven { url 'https://noams.artifactoryonline.com/noams/grails-jaxrs-plugin-snapshots' }
    jcenter()
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    resolve {
        repoKey = 'libs-release'
    }
    publish {
        defaults {
            publishArtifacts = true
        }
    }
}

def buildingForGrails22 = hasProperty('grails2.2')
def revisionExtension = ''

if (buildingForGrails22) {
    ext.spockSupportVersion = '0.7-groovy-2.0'
    ext.grailsVersion = '2.2.0'
    ext.groovyVersion = '2.0.7'
    revisionExtension = '-grails2.2'
} else {
    ext.spockSupportVersion = '0.7-groovy-1.8'
    ext.grailsVersion = '2.0.0'
    ext.groovyVersion = '1.8.8'
}

group = 'org.10ne.grails'
version = "0.0.1$revisionExtension-SNAPSHOT"

configurations {
    provided
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

dependencies {
    provided "org.codehaus.groovy:groovy-all:${groovyVersion}"
    ['core', 'web', 'plugin-gsp'].each {
        provided("org.grails:grails-$it:${grailsVersion}")
    }
    if (buildingForGrails22) {
        ['mimetypes', 'controllers', 'converters'].each {
            provided("org.grails:grails-plugin-$it:${grailsVersion}")
        }
    }
    provided("org.spockframework:spock-grails-support:${spockSupportVersion}")
    provided('org.grails.plugins:jaxrs:0.9-binary-SNAPSHOT') {
        transitive = false
    }
    provided 'javax.servlet:servlet-api:2.4'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        dist(MavenPublication) {

            from components.java
            artifact sourceJar {
                classifier 'source'
            }
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.dist)
}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.6'
    }
    module {
        scopes.PROVIDED.plus += configurations.provided
    }
}

task wrapper(type: Wrapper, description: 'Gradle Wrapper task') {
    gradleVersion = '1.8'
}
